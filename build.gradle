
plugins {
    id 'java'
    id 'maven'
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
}

apply from: 'gradle/mavenCentralPublishing.gradle'

scmVersion {
    tag {
        prefix = 'polyjdbc'
    }
    versionCreator 'versionWithBranch'
}

project.group = 'org.polyjdbc'
project.version = scmVersion.version

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'

    testCompile group: 'ch.qos.logback', name: 'logback-core', version: '1.0.13'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'

    testCompile group: 'org.testng', name: 'testng', version: '6.8.1'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '1.6.1'
    testCompile group: 'com.googlecode.catch-exception', name: 'catch-exception', version: '1.2.0'
    testCompile group: 'joda-time', name: 'joda-time', version: '2.3'

    testCompile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'
    testCompile group: 'com.h2database', name: 'h2', version: '1.3.175'
    testCompile group: 'org.postgresql', name: 'postgresql', version: '9.2-1003-jdbc4'
    testCompile group: 'mysql', name: 'mysql-connector-java', version: '5.1.26'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

test {
    useTestNG()

    options {
        suiteXmlFiles = [ project.file('src/test/resources/testng.xml') ]
    }

    if(project.hasProperty('integration')) {
        options {
            suiteXmlFiles = [ project.file('src/test/resources/testng.xml'), project.file('src/test/resources/testng-integration.xml') ]
        }
    }

    //gradlew clean test -Pintegration-oracle
    if(project.hasProperty('integration-oracle')) {
        dependencies{
            testCompile files('libs/ojdbc6.jar') //ojdbc6.jar should be manually copied to project /libs and .gitingored
        }
        options {
            suiteXmlFiles = [ project.file('src/test/resources/testng-integration-oracle.xml') ]
        }
    }
    //gradlew clean test -Pintegration-mssql
    if(project.hasProperty('integration-mssql')) {
        dependencies{
            testCompile files('libs/sqljdbc4.jar') //sqljdbc4.jar should be manually copied to project /libs and .gitingored
        }
        options {
            suiteXmlFiles = [ project.file('src/test/resources/testng-integration-mssql.xml') ]
        }
    }

    testLogging {
        exceptionFormat = 'full'
    }
}

